apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def currentVersion = "1.0.0"

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 1
        versionName currentVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile libLogging
    compile libAppCompat
}
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += project.files(configurations.compile)
//    excludes = ['com.mastertechsoftware.logging.*']
    println(classpath.getFiles())
    failOnError false
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'tasker'
            description 'Library for UI and background tasks'
            url 'https://github.com/kevindmoore/tasker'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.mastertechsoftware.tasker'
            artifactId 'tasker'
            version currentVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/kevindmoore/tasker.git'
                url 'https://github.com/kevindmoore/tasker'

            }
            developers {
                developer {
                    name 'Kevin Moore'
                }
            }
        }
    }
}

group = 'com.mastertechsoftware.tasker'
version = currentVersion

bintray {
    user = bintray_user
    key = bintray_key
    dryRun = false //Whether to run this as dry-run, without deploying
    pkg {
        repo = 'maven'
        name = 'tasker'

        labels = ['android']
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kevindmoore/tasker.git'
        websiteUrl = 'https://github.com/kevindmoore/tasker'
    }
    configurations = ['archives']
}
